plugins {
    // https://plugins.gradle.org/plugin/org.springframework.boot
    id 'org.springframework.boot' version '2.7.0' apply false
    // https://plugins.gradle.org/plugin/io.spring.dependency-management
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
}

subprojects {
    apply plugin: 'java-library'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    version = '0.1'
    group = 'org.dreamcat.daily.script'

    ext {
        // https://mvnrepository.com/artifact/org.projectlombok/lombok
        lombokVersion = '1.18.24'
        // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
        slf4jVersion = '1.7.36'
        // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
        jupiterVersion = '5.8.2'
        // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
        jacksonVersion = '2.13.3'
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"

        implementation "org.dreamcat:common-core:0.3"
    }

    task printVersion {
        doLast {
            print version
        }
    }

    test {
        useJUnitPlatform()
    }

    if (file('../../tasks.gradle').exists()) {
        apply from: "../../tasks.gradle"
    }

}

